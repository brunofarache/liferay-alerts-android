apply plugin: 'com.android.application'

android {
	compileSdkVersion 20
	buildToolsVersion '20.0.0'

	defaultConfig {
		applicationId 'com.liferay.alerts'
		minSdkVersion 16
		targetSdkVersion 20
		versionCode 3
		versionName '1.0'
	}

	packagingOptions {
		exclude 'META-INF/LICENSE'
		exclude 'META-INF/NOTICE'
	}

	signingConfigs {
		release {
		}
	}

	buildTypes {
		release {
			signingConfig signingConfigs.release
		}
	}
}

if (hasProperty('password')) {
	android.signingConfigs.release.storeFile file('../../alerts-release.keystore')
	android.signingConfigs.release.storePassword password
	android.signingConfigs.release.keyAlias 'alerts'
	android.signingConfigs.release.keyPassword password
}

repositories {
	maven {
		url 'https://repository.liferay.com/nexus/content/groups/liferay-ce'
	}
}

configurations {
	formatter
}

dependencies {
	compile group: 'com.android.support', name: 'appcompat-v7', version: '20.+'
	compile group: 'com.google.android.gms', name: 'play-services', version: '4.2.42'
	compile group: 'com.liferay.mobile', name: 'liferay-android-sdk', version: '6.2.0.6'
	compile group: 'com.squareup.picasso', name: 'picasso', version: '2.3.3'

	formatter group: 'com.liferay.portal', name: 'portal-impl', version: '6.2.0-ga1'
	formatter group: 'com.liferay.portal', name: 'portal-service', version: '6.2.0-ga1'
	formatter group: 'com.liferay.portal', name: 'util-java', version: '6.2.0-ga1'
	formatter group: 'dom4j', name: 'dom4j', version: '1.6.1'
	formatter group: 'org.apache.ant', name: 'ant', version: '1.8.2'
}

task format(type: JavaExec) {
	main = 'com.liferay.portal.tools.sourceformatter.SourceFormatter'
	classpath = configurations.formatter
	workingDir = 'src'
}